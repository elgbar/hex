import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import java.nio.file.Files
import java.nio.file.Paths

buildscript {
  ext {
    appName = "Hex"
    kotlinVersion = "2.1.21"
    gdxVersion = '1.13.5'
    ktxVersion = "1.13.1-rc1"
    visVersion = '1.5.5'
    jacksonVersion = "2.19.0"
    junitVersion = "5.12.2"
    kotlinCoroutineVersion = "1.10.2"
    argparserVersion = "2.0.7"
    spotlessVersion = "7.0.3"
    androidGradleToolsVersion = '8.10.0'
    ktlintVersion = "0.48.2"
    gradleVersions = "0.52.0"
  }

  repositories {
    mavenLocal()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    mavenCentral()
    google()
    gradlePluginPortal()
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotlessVersion"
    classpath "com.android.tools.build:gradle:$androidGradleToolsVersion"
    classpath "com.github.ben-manes:gradle-versions-plugin:$gradleVersions"
  }
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

subprojects {
  apply plugin: "com.diffplug.spotless"

  version = hexVersion

  repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
    google()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
  }

  spotless {
    kotlin {
      targetExclude fileTree(projectDir) {
        include '**/noise/**'
        include '**/hexworks/**'
      }

      ktlint()
        .setEditorConfigPath("$rootDir/.editorconfig")
        .editorConfigOverride([
          "ktlint_function_signature_rule_force_multiline_when_parameter_count_greater_or_equal_than": "5",
          "ktlint_function_signature_body_expression_wrapping"                                       : "multiline"
        ])
    }
    format 'misc', {
      // define the files to apply `misc` to
      target '*.kt', '*.gradle', '*.md', '.gitignore'

      // define the steps to apply to those files
      trimTrailingWhitespace()
      leadingTabsToSpaces(2)
      endWithNewline()
    }
  }

  tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
      progressiveMode = true
      optIn = ["kotlin.contracts.ExperimentalContracts"]
      freeCompilerArgs.add("-Xwhen-guards") // https://github.com/Kotlin/KEEP/blob/guards/proposals/guards.md
      freeCompilerArgs.add("-Xnon-local-break-continue") // https://github.com/Kotlin/KEEP/blob/guards/proposals/break-continue-in-inline-lambdas.md (beta in 2.1)
//      allWarningsAsErrors = true
    }
  }
}

allprojects {
  apply plugin: "com.github.ben-manes.versions"

  tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
      isNonStable(it.candidate.version)
    }
  }

}

tasks.named('wrapper') {
  distributionType = Wrapper.DistributionType.BIN
}

project(":android") {
  apply plugin: "com.android.application"
  apply plugin: 'kotlin-android'

  configurations { natives }

  dependencies {
    implementation project(":core")

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutineVersion"

    implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86_64"

    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86_64"

  }
}

project(":desktop") {

  apply plugin: "kotlin"

  dependencies {
    implementation project(":core")
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
  }

  test {
    useJUnitPlatform()
  }

  jar {
    duplicatesStrategy = DuplicatesStrategy.WARN
  }

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(javaVersion.toInteger())
    }
  }

  classes {
    doFirst {
      // Automatically create a soft link to the island folder to allow map-editor to work
      // On windows developer mode must be turned on for this to work
      // This file is only used when map editor is also used, you can play the game without this file existing
      var root = rootDir.path
      var targetPath = Paths.get(root, "core", "src", "main", "resources", "islands")
      var link = Paths.get(root, "islands")
      if (Files.notExists(link)) {
        try {
          Files.createSymbolicLink(link, targetPath)
        } catch (IOException e) {
          System.err.println("Failed to create symbolic link. Try enable developer mode: https://blogs.windows.com/windowsdeveloper/2016/12/02/symlinks-windows-10/.\n $e.class.simpleName: $e.message")
        }
      }
    }
  }
}

project(":core") {
  apply plugin: "kotlin"

  classes {

    dependsOn spotlessApply
    doFirst {
      layout.buildDirectory.file("resources/main/version.properties").get().asFile.withWriter { writer ->
        Properties properties = new Properties()
        properties['version'] = hexVersion
        properties.store writer, null
      }
    }
  }

  dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    api "com.xenomachina:kotlin-argparser:$argparserVersion"

    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    api "com.kotcrab.vis:vis-ui:$visVersion"
    api platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:$kotlinCoroutineVersion")
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutineVersion"
    api "org.jetbrains:annotations:26.0.2"

    api "io.github.libktx:ktx-app:$ktxVersion"
    implementation "io.github.libktx:ktx-vis:$ktxVersion"
    implementation "io.github.libktx:ktx-vis-style:$ktxVersion"
    implementation "io.github.libktx:ktx-style:$ktxVersion"
    implementation "io.github.libktx:ktx-actors:$ktxVersion"
    implementation "io.github.libktx:ktx-collections:$ktxVersion"
    implementation "io.github.libktx:ktx-graphics:$ktxVersion"
    implementation "io.github.libktx:ktx-async:$ktxVersion"
    implementation "io.github.libktx:ktx-assets:$ktxVersion"

    api "com.badlogicgames.gdx:gdx-tools:$gdxVersion"

    api platform("com.fasterxml.jackson:jackson-bom:$jacksonVersion")
    api "com.fasterxml.jackson.module:jackson-module-kotlin"
    api "com.fasterxml.jackson.dataformat:jackson-dataformat-smile"

    implementation 'org.tukaani:xz:1.10'

    testApi "org.jetbrains.kotlin:kotlin-test-junit5:$kotlinVersion"
    testApi("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testApi "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    testApi "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junitVersion")

    constraints {
      implementation("net.java.jinput:jinput:2.0.10")
    }
  }

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(javaVersion.toInteger())
    }
  }
}
