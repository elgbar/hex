buildscript {

  ext {
    kotlinVersion = "1.4.0"
    appName = "Hex"
    gdxVersion = '1.9.11'
    roboVMVersion = '2.3.8'
    ashleyVersion = '1.7.3'
    aiVersion = '1.8.2'
    visVersion = '1.4.6'
    kiwiVersion = '1.9.1.9.6'
    jacksonVersion = "2.11.1"
    junitVersion = "5.4.2"
    ktxVersion = "1.9.11-b1"
  }

  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    jcenter()
    google()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:5.1.1"
  }
}

subprojects {
  apply plugin: "kotlin"
  apply plugin: "idea"
  apply plugin: "com.diffplug.spotless"

  version = '1.0'

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    google()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url 'https://jitpack.io' }
  }

  [compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "1.8"
  [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

  test {
    useJUnitPlatform()
  }

  spotless {
    kotlin {
      ktlint("0.39.0").userData([
         "indent_size"             : "2",
         "continuation_indent_size": "2",
         "max_line_length"         : "160"
      ])
    }
    groovyGradle {
      target '*.gradle' // default target of groovyGradle
      greclipse()
    }
    format 'misc', {
      // define the files to apply `misc` to
      target '*.kt', '*.gradle', '*.md', '.gitignore'

      // define the steps to apply to those files
      trimTrailingWhitespace()
      indentWithSpaces(2)
      endWithNewline()
    }
  }
  assemble.dependsOn(spotlessApply)
}

tasks.named('wrapper') {
  distributionType = Wrapper.DistributionType.ALL
}

subprojects {
  dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    api "com.xenomachina:kotlin-argparser:2.0.7"
  }
}

project(":desktop") {
  apply plugin: "kotlin"

  dependencies {
    api project(":core")
    api "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    api "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    api "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
  }
}

project(":core") {
  apply plugin: "kotlin"

  dependencies {
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-ai:$aiVersion"
    api "com.kotcrab.vis:vis-ui:$visVersion"
    api "com.github.Hexworks.mixite:mixite.core-jvm:2020.1.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8"

    implementation "io.github.libktx:ktx-vis:$ktxVersion"
    implementation "io.github.libktx:ktx-vis-style:$ktxVersion"
    implementation "io.github.libktx:ktx-style:$ktxVersion"
    implementation "io.github.libktx:ktx-actors:$ktxVersion"

    api "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"

    testApi "org.jetbrains.kotlin:kotlin-test-junit5:$kotlinVersion"
    testApi("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
  }
}
