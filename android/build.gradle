project.ext.mainClassName = "no.elg.hex.desktop.AndroidLauncher"
project.ext.assetsDir = new File("../core/src/main/resources")

int androidSdkVersion = 36
int androidVersionCode = Integer.parseInt(androidVersionCode)
int javaCompileVersion = Integer.parseInt(javaVersion)

android {
  compileSdk androidSdkVersion

  namespace = 'no.elg.hex'

  compileOptions {
    sourceCompatibility = JavaVersion.toVersion(javaCompileVersion)
    targetCompatibility = JavaVersion.toVersion(javaCompileVersion)
  }

  sourceSets {
    main {
      assets.srcDirs = [
        'assets',
        '../core/src/main/resources'
      ]
      jniLibs.srcDirs = ['libs']
    }
  }
  var fullVersion = "$hexVersion-avc$androidVersionCode"
  packagingOptions {
    resources {
      excludes += ['META-INF/robovm/ios/robovm.xml']
      excludes += ['islands/**']
      excludes += ['sprites/**']
      excludes += ['fonts/**']
      excludes += ['sounds/**']
      excludes += ['icons/**']
      excludes += ['shaders/**']
      excludes += ['music/**']
    }
  }
  defaultConfig {
    applicationId "no.elg.hex"
    minSdkVersion 27
    targetSdkVersion androidSdkVersion
    versionCode androidVersionCode
    versionName fullVersion
  }
  buildTypes {
    release {
      minifyEnabled false
      resValue("string", "version", hexVersion)
      resValue("string", "args", "")
    }
    debug {
      minifyEnabled false

      resValue("string", "version", hexVersion)
      resValue("string", "args", "")
//      resValue("string", "args", "--trace")
    }
  }
  buildFeatures {
    resValues = true
  }

  clean {
    delete("libs", "release")
  }
}

// called every time gradle gets executed, takes the native dependencies of
// the natives configuration, and extracts them to the proper libs/ folders
// so they get packed with the APK.
tasks.register('copyAndroidNatives') {
  doFirst {

    configurations.natives.copy().files.each { jar ->
      def outputDir = null
      if (jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("libs/arm64-v8a")
      if (jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")
      if (jar.name.endsWith("natives-x86_64.jar")) outputDir = file("libs/x86_64")
      if (jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")
      if (outputDir != null) {
        outputDir.mkdirs()
        copy {
          from zipTree(jar)
          into outputDir
          include "*.so"
        }
      }
    }
  }
}

tasks.configureEach { packageTask ->
  if (packageTask.name.contains("package")) {
    packageTask.dependsOn 'copyAndroidNatives'
  }
}